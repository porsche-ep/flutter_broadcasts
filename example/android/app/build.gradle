plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "de.kevlatus.flutter_broadcasts_example"

    compileSdkVersion 35

    ndkVersion "27.0.12077973"

    defaultConfig {
        applicationId "de.kevlatus.flutter_broadcasts_example"

        minSdkVersion 24
        targetSdkVersion 35

        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'com.fazua.fazuatoolbox'
        ]
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            // crash BUG in x86
            // https://developer.android.com/ndk/guides/abis#gradle
            // https://github.com/flutter/flutter/issues/83596
            ndk { abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64' }
        }
    }

    flavorDimensions "app"
    productFlavors {
        development {
            dimension "app"
        }

        staging {
            dimension "app"
        }

        production {
            dimension "app"
        }
    }

    lint {
        disable 'InvalidPackage'
    }
}

flutter {
    source '../..'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "com.polidea.rxandroidble2:rxandroidble:1.18.1"
}
